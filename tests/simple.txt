*** Setting ***

Library  HttpLibrary.HTTP
Library  OperatingSystem

Suite Setup     Start Mockserver
Suite Teardown  Stop Mockserver

*** Variables ***

${HOST}  localhost:36503

*** Test Cases ***

Error if GET before connect
    ${msg}=             Run Keyword And Expect Error  *  GET   /
    Should Start With   ${msg}    Not connected to any HTTP Host

Simple GET
    [Setup]  Connect  ${HOST}
    GET      /200

GET 200
    [Setup]  Connect  ${HOST}
    GET      /200
    Response Status Code Should Equal   200

GET Body
    [Setup]  Connect  ${HOST}
    GET      /200
    Response Body Should Contain  Everything is ok

GET 302
    [Setup]  Connect  ${HOST}
    GET      /302
    Response Status Code Should Equal   302

GET 302 with follow
    [Setup]  Connect  ${HOST}
    GET      /302
    Follow Response
    Response Status Code Should Equal   200

GET 418
    [Setup]  Connect  ${HOST}
    GET      /418
    Response Status Code Should Equal   418 I'm a teapot

Response Should Succeed OK
    [Setup]  Connect  ${HOST}
    GET      /200
    Response Should Succeed

Response Should Succeed FAIL
    [Setup]  Connect  ${HOST}
    GET      /404
    Run Keyword And Expect Error  *  Response Should Succeed
    
Response Should Not Succeed OK
    [Setup]  Connect  ${HOST}
    GET      /503
    Response Should Not Succeed

Response Should Not Succeed FAIL
    [Setup]  Connect  ${HOST}
    GET      /200
    Run Keyword And Expect Error  *  Response Should Not Succeed

Response Should Have Header OK
    [Setup]  Connect  ${HOST}
    GET      /302
    Response Should Have Header  Location

Response Should Have Header FAIL
    [Setup]  Connect  ${HOST}
    GET      /200
    Run Keyword And Expect Error  *  Response Should Have Header  Krytzmyk

Response Should Not Have Header OK
    [Setup]  Connect  ${HOST}
    GET      /404
    Response Should Not Have Header  Location

Response Should Not Have Header FAIL
    [Setup]  Connect  ${HOST}
    GET      /302
    Run Keyword And Expect Error  *  Response Should Not Have Header  Location

Response Header Should Equal OK
    [Setup]  Connect  ${HOST}
    GET      /302
    Response Header Should Equal   Location  /200

Response Header Should Equal FAIL
    [Setup]  Connect  ${HOST}
    GET      /302
    Run Keyword And Expect Error  *  Response Header Should Equal   Location  http://some.other.host/

Response Header Should Not Equal OK
    [Setup]  Connect  ${HOST}
    GET      /302
    Response Header Should Not Equal   Location  http://and.another.host/

Response Header Should Not Equal FAIL
    [Setup]  Connect  ${HOST}
    GET      /302
    Run Keyword And Expect Error  *  Response Header Should Not Equal   Location  /200

Simple HEAD request
    [Setup]  Connect  ${HOST}
    HEAD     /head
    Response Should Not Succeed
    Response Status Code Should Equal   407
    Log Response Headers                WARN

Simple DELETE request
    [Setup]  Connect  ${HOST}
    DELETE    /method
    Response Should Succeed

Simple POST request
    [Setup]  Connect  ${HOST}
    POST      /echo
    Response Should Succeed

POST with one word request body
    [Setup]  Connect              ${HOST}
    Set Request Body              Tooot
    Set Request Header            Content-Type    text/plain
    POST                          /echo
    Response Body Should Contain  Tooot

PUT with two word request body
    [Setup]  Connect              ${HOST}
    Set Request Body              Tooot Tooooot
    Set Request Header            Content-Type    text/plain
    PUT                           /echo
    Response Body Should Contain  Tooot Tooooot

Get Response Body
    [Setup]  Connect              ${HOST}
    Set Request Body              1234567890
    Set Request Header            Content-Type    text/plain
    POST                          /echo
    ${body}=                      Get Response Body
    Should Contain    ${body}     1234567890

Log Response Body
    [Setup]  Connect              ${HOST}
    Set Request Body              Log me! Please!
    Set Request Header            Content-Type    text/plain
    POST                          /echo
    Log Response Body             WARN

Set Request Header
    [Setup]  Connect                    ${HOST}
    Set Request Header                  If-Modified-Since  February
    GET                                 /304
    Response Status Code Should Equal   304

Set Content-Type Request Header
    [Setup]  Connect                    ${HOST}
    Set Request Header                  Content-Type  image/gif
    POST                                /content_type
    Response Status Code Should Equal   200
    Response Body Should Contain        image/gif

Set Basic Auth
    [Setup]  Connect                    ${HOST}
    GET                                 /sesame
    Response Should Not Succeed

    Set Basic Auth                      Aladdin        open sesame
    GET                                 /sesame
    Response Status Code Should Equal   200
    Response Body Should Contain        sesame street

*** Keywords ***

Start Mockserver
    Start Process  tests/mockserver.py
    Sleep  0.2

Stop Mockserver
    Connect  ${HOST}
    POST     /kill

