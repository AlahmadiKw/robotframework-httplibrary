*** Variables ***

${HOST}  localhost:36503

*** Setting ***

Library  HttpLibrary.HTTP
Library  OperatingSystem

Suite Setup     Start Mockserver
Suite Teardown  Stop Mockserver

Test Setup      Set HTTP Host  ${HOST}

*** Test Cases ***

Error if GET before connect
    [Setup]  No Operation
    Run Keyword And Expect Error
    ...  Not connected to any HTTP Host. Use "Set HTTP Host" keyword first.
    ...  GET   /

Error if invalid URL
    [Setup]  No Operation
    Run Keyword And Expect Error
    ...  "NEITHER_URL_NOR_PATH" needs to be in form of "/path" or "http://host/path"
    ...  GET   NEITHER_URL_NOR_PATH

Simple GET
    GET      /200

Simple GET with complete host
    [Setup]  No Operation
    GET      http://${HOST}/200

GET 200
    GET      /200
    Response Status Code Should Equal   200

GET Body
    GET      /200
    Response Body Should Contain  Everything is ok

GET 302
    GET      /302
    Response Status Code Should Equal   302

GET 302 with follow
    GET      /302
    Follow Response
    Response Status Code Should Equal   200

GET 418
    GET      /418
    Response Status Code Should Equal   418 I'm a teapot

Response Should Succeed OK
    GET      /200
    Response Should Succeed

Response Should Succeed FAIL
    GET      /404
    Run Keyword And Expect Error
    ...  Response should have succeeded, but was "404 Not Found".
    ...  Response Should Succeed

Response Should Not Succeed OK
    GET      /503
    Response Should Not Succeed

HTTP Request with custom HTTP verb should work
    HTTP Request  PATCH  /patch
    Response Body Should Contain  Got a patch request

Response Should Not Succeed FAIL
    GET      /200
    Run Keyword And Expect Error
    ...  Response should not have succeeded, but was "200 OK".
    ...  Response Should Not Succeed

Response Should Have Header OK
    GET      /302
    Response Should Have Header  Location

Response Should Have Header FAIL
    GET      /200
    Run Keyword And Expect Error
    ...  Response did not have "Krytzmyk" header, but should have.
    ...  Response Should Have Header  Krytzmyk

Response Should Not Have Header OK
    GET      /404
    Response Should Not Have Header  Location

Response Should Not Have Header FAIL
    GET      /302
    Run Keyword And Expect Error
    ...  Response did have "Location" header, but should not have.
    ...  Response Should Not Have Header  Location

Response Header Should Equal OK
    GET      /302
    Response Header Should Equal   Location  /200

Response Header Should Equal FAIL
    GET      /302
    Run Keyword And Expect Error
    ...  Response header "Location" should have been "http://some.other.host/" but was "/200".
    ...  Response Header Should Equal   Location  http://some.other.host/

Response Header Should Not Equal OK
    GET      /302
    Response Header Should Not Equal   Location  http://and.another.host/

Response Header Should Not Equal FAIL
    GET      /302
    Run Keyword And Expect Error
    ...  Response header "Location" was "/200" but should not have been.
    ...  Response Header Should Not Equal   Location  /200

Simple HEAD request
    HEAD     /head
    Response Should Not Succeed
    Response Status Code Should Equal   407
    Log Response Headers                WARN

Simple DELETE request
    DELETE   /method
    Response Should Succeed

Simple POST request
    POST      /echo
    Response Should Succeed

POST with one word request body
    Set Request Body              Tooot
    Set Request Header            Content-Type    text/plain
    POST                          /echo
    Response Body Should Contain  Tooot

PUT with two word request body
    Set Request Body              Tooot Tooooot
    Set Request Header            Content-Type    text/plain
    PUT                           /echo
    Response Body Should Contain  Tooot Tooooot

Get Response Body
    Set Request Body              1234567890
    Set Request Header            Content-Type    text/plain
    POST                          /echo
    ${body}=                      Get Response Body
    Should Contain    ${body}     1234567890

Log Response Body
    Set Request Body              Log me! Please!
    Set Request Header            Content-Type    text/plain
    POST                          /echo
    Log Response Body             WARN

Set Request Header
    Set Request Header                  If-Modified-Since  February
    GET                                 /304
    Response Status Code Should Equal   304

Set Content-Type Request Header
    Set Request Header                  Content-Type  image/gif
    POST                                /content_type
    Response Status Code Should Equal   200
    Response Body Should Contain        image/gif

Set Basic Auth
    GET                                 /sesame
    Response Should Not Succeed

    Set Basic Auth                      Aladdin        open sesame
    GET                                 /sesame
    Response Status Code Should Equal   200
    Response Body Should Contain        sesame street

*** Keywords ***

Start Mockserver
    Start Process  tests/mockserver.py
    Sleep  0.2

Stop Mockserver
    Set HTTP Host  ${HOST}
    POST           /kill

