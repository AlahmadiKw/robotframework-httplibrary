*** Variables ***

${HOST}  localhost:36503

*** Setting ***

Library  HttpLibrary.HTTP
Library  OperatingSystem

Suite Setup     Start Mockserver
Suite Teardown  Stop Mockserver

Test Setup      Set HTTP Host  ${HOST}

*** Test Cases ***

Error if GET before connect
    [Setup]  No Operation
    Run Keyword And Expect Error
    ...  Not connected to any HTTP Host. Use "Set HTTP Host" keyword first.
    ...  GET   /

Error if invalid URL
    [Setup]  No Operation
    Run Keyword And Expect Error
    ...  "NEITHER_URL_NOR_PATH" needs to be in form of "/path" or "http://host/path"
    ...  GET   NEITHER_URL_NOR_PATH

Simple GET
    GET      /200

Simple GET with complete host
    [Setup]  No Operation
    GET      http://${HOST}/200

GET 200
    GET      /200
    Response Status Code Should Equal   200

GET Body
    GET      /200
    Response Body Should Contain  Everything is ok

GET 302
    GET      /302
    Response Status Code Should Equal   302

GET 302 with follow
    GET      /302
    Follow Response
    Response Status Code Should Equal   200

GET 418
    Next Request Should Not Succeed
    GET      /418
    Response Status Code Should Equal   418 I'm a teapot

Implicit Next Request Should Succeed
    Run Keyword And Expect Error
    ...  Request should have succeeded, but was "404 Not Found".
    ...  GET      /404

Next Request May Not Succeed
    Next Request May Not Succeed
    GET       /404  # no error

Explicit Next Request Should Succeed
    Next Request Should Succeed
    Run Keyword And Expect Error
    ...  Request should have succeeded, but was "404 Not Found".
    ...  GET      /404

Next Request Should Have Status Code OK
    Next Request Should Have Status Code  418
    GET       /418  # no error, correct status code

Next Request Should Have Status Code FAIL
    Next Request Should Have Status Code  201
    Run Keyword And Expect Error
    ...  "418 I'm a teapot" does not start with "201", but should have.
    ...  GET      /418

Next Request Should Have Status Code w/ Status Line OK
    Next Request Should Have Status Code  418 I'm a teapot
    GET       /418  # no error, correct status code

Next Request Should Have Status Code w/ Status Line FAIL
    Next Request Should Have Status Code  418 I'm a teaCUP
    Run Keyword And Expect Error
    ...  "418 I'm a teapot" does not start with "418 I'm a teaCUP", but should have.
    ...  GET      /418

Response Should Succeed OK
    GET      /200
    Response Should Succeed

Response Should Succeed FAIL
    Next Request Should Not Succeed
    GET      /404
    Run Keyword And Expect Error
    ...  Response should have succeeded, but was "404 Not Found".
    ...  Response Should Succeed

Response Should Not Succeed OK
    Next Request May Not Succeed
    GET      /503
    Response Should Not Succeed

HTTP Request with custom HTTP verb should work
    HTTP Request  PATCH  /patch
    Response Body Should Contain  Got a patch request

Response Should Not Succeed FAIL
    GET      /200
    Run Keyword And Expect Error
    ...  Response should not have succeeded, but was "200 OK".
    ...  Response Should Not Succeed

Response Should Have Header OK
    GET      /302
    Response Should Have Header  Location

Response Should Have Header FAIL
    GET      /200
    Run Keyword And Expect Error
    ...  Response did not have "Krytzmyk" header, but should have.
    ...  Response Should Have Header  Krytzmyk

Response Should Not Have Header OK
    GET      /200
    Response Should Not Have Header  Location

Response Should Not Have Header FAIL
    GET      /302
    Run Keyword And Expect Error
    ...  Response did have "Location" header, but should not have.
    ...  Response Should Not Have Header  Location

Response Header Should Equal OK
    GET      /302
    Response Header Should Equal   Location  /200

Response Header Should Equal FAIL
    GET      /302
    Run Keyword And Expect Error
    ...  Response header "Location" should have been "http://some.other.host/" but was "/200".
    ...  Response Header Should Equal   Location  http://some.other.host/

Response Header Should Not Equal OK
    GET      /302
    Response Header Should Not Equal   Location  http://and.another.host/

Response Header Should Not Equal FAIL
    GET      /302
    Run Keyword And Expect Error
    ...  Response header "Location" was "/200" but should not have been.
    ...  Response Header Should Not Equal   Location  /200

Simple HEAD request
    HEAD     /head

Log Response Headers
    [Documentation]
    ...  LOG 2:1   REGEXP: ^date: .+
    ...  LOG 2:2   x-powered-by: PHP
    HEAD     /head
    Log Response Headers

Simple DELETE request
    DELETE   /method

Simple POST request
    POST      /echo

POST with one word request body
    Set Request Body              Tooot
    Set Request Header            Content-Type    text/plain
    POST                          /echo
    Response Body Should Contain  Tooot

PUT with two word request body
    Set Request Body              Tooot Tooooot
    Set Request Header            Content-Type    text/plain
    PUT                           /echo
    Response Body Should Contain  Tooot Tooooot

Get Response Body
    Set Request Body              1234567890
    Set Request Header            Content-Type    text/plain
    POST                          /echo
    ${body}=                      Get Response Body
    Should Contain    ${body}     1234567890

Log Response Body
    [Documentation]
    ...                 LOG 4:1   Hallo!
    Set Request Body              Hallo!
    Set Request Header            Content-Type    text/plain
    POST                          /echo
    Log Response Body

Set Request Header
    Set Request Header                  If-Modified-Since  February
    GET                                 /304
    Response Status Code Should Equal   304

Set Content-Type Request Header
    Set Request Header                  Content-Type  image/gif
    POST                                /content_type
    Response Status Code Should Equal   200
    Response Body Should Contain        image/gif

Set Basic Auth
    Next Request Should Not Succeed
    GET                                 /sesame

    Set Basic Auth                      Aladdin        open sesame
    GET                                 /sesame
    Response Status Code Should Equal   200
    Response Body Should Contain        sesame street

*** Keywords ***

Start Mockserver
    Start Process  tests/mockserver.py
    Sleep  0.2

Stop Mockserver
    Set HTTP Host  ${HOST}
    POST           /kill

